d20p1:{a:"\n"vs x;
    label:([]lb:`$();ci:`long$();cj:`long$());
    ll:a within "AZ";
    lld:ll and (-1_enlist[count[first a]#0b],ll) and ((-2_(2#enlist(count first a)#" "),a)=".");
    lbd:raze til[count lld],/:'where each lld;
    label,:([]lb:`$a ./:/:(-1 0;0 0)+\:/:lbd;ci:lbd[;0]-2;cj:lbd[;1]);
    llu:ll and ((1_ll),enlist[count[first a]#0b]) and (((2_a),2#enlist(count first a)#" ")=".");
    lbu:raze til[count lld],/:'where each llu;
    label,:([]lb:`$a ./:/:(0 0;1 0)+\:/:lbu;ci:lbu[;0]+2;cj:lbu[;1]);
    lll:ll and (0b,/:-1_/:ll) and "."=("  ",/:-2_/:a);
    lbl:raze til[count lll],/:'where each lll;
    label,:([]lb:`$a ./:/:(0 -1;0 0)+\:/:lbl;ci:lbl[;0];cj:lbl[;1]-2);
    llr:ll and ((1_/:ll),\:0b) and "."=((2_/:a),\:"  ");
    lbr:raze til[count llr],/:'where each llr;
    label,:([]lb:`$a ./:/:(0 0;0 1)+\:/:lbr;ci:lbr[;0];cj:lbr[;1]+2);
    start:exec (first ci;first cj) from label where lb=`AA;
    finish:exec (first ci;first cj) from label where lb=`ZZ;
    parent:enlist[start]!enlist 0N 0N;
    warp:raze exec {x!x except/:enlist each x}each cs from select cs:flip(ci;cj) by lb from label;
    queue:enlist start;
    while[count queue;
        nxts:(queue+/:\:(-1 0;0 1;1 0;0 -1)),'warp queue;
        nxtt:a ./:/:nxts;
        nxts:(nxts@'where each "."=nxtt)except\:value parent;
        parent[raze nxts]:raze (count each nxts)#'enlist each queue;
        queue:raze nxts;
        if[0<count arrive:where finish~/:queue;
            :count 1_-2_parent\[first queue arrive]
        ];
    ];
    "not found"};

d20p2:{a:"\n"vs x;
    label:([]lb:`$();ci:`long$();cj:`long$();dk:`long$());
    ll:a within "AZ";
    lld:ll and (-1_enlist[count[first a]#0b],ll) and ((-2_(2#enlist(count first a)#" "),a)=".");
    lbd:raze til[count lld],/:'where each lld;
    label,:([]lb:`$a ./:/:(-1 0;0 0)+\:/:lbd;ci:lbd[;0]-2;cj:lbd[;1];dk:?[lbd[;0]<count[a]div 2;-1;1]);
    llu:ll and ((1_ll),enlist[count[first a]#0b]) and (((2_a),2#enlist(count first a)#" ")=".");
    lbu:raze til[count lld],/:'where each llu;
    label,:([]lb:`$a ./:/:(0 0;1 0)+\:/:lbu;ci:lbu[;0]+2;cj:lbu[;1];dk:?[lbu[;0]<count[a] div 2;1;-1]);
    lll:ll and (0b,/:-1_/:ll) and "."=("  ",/:-2_/:a);
    lbl:raze til[count lll],/:'where each lll;
    label,:([]lb:`$a ./:/:(0 -1;0 0)+\:/:lbl;ci:lbl[;0];cj:lbl[;1]-2;dk:?[lbl[;1]<count[first a]div 2;-1;1]);
    llr:ll and ((1_/:ll),\:0b) and "."=((2_/:a),\:"  ");
    lbr:raze til[count llr],/:'where each llr;
    label,:([]lb:`$a ./:/:(0 0;0 1)+\:/:lbr;ci:lbr[;0];cj:lbr[;1]+2;dk:?[lbr[;1]<count[first a]div 2;1;-1]);
    start:exec (first ci;first cj;0) from label where lb=`AA;
    finish:exec (first ci;first cj;0) from label where lb=`ZZ;
    warp:raze exec {(-1_/:x)!x except/:enlist each x}each cs from select cs:flip(ci;cj;dk) by lb from label;

    paths:([]sl:();tl:();plen:`long$());
    queue:select ls:(lb,'dk),pos:(ci,'cj) from label;
    parent:exec (ls,'pos)!count[i]#enlist 0N 0N from queue;
    while[0<count queue;
        nxts:update npos:pos+/:\:(-1 0;0 1;1 0;0 -1) from queue;
        nxts2:raze{([]ls:count[x`npos]#enlist x`ls;pos:count[x`npos]#enlist x`pos;npos:x`npos)}each nxts;
        nxts2:update ntile:a ./:npos from nxts2;
        nxts2:select from nxts2 where ntile=".", not (ls,'npos) in key parent;
        parent[exec (ls,'npos) from nxts2]:exec pos from nxts2;
        if[0<count found:select from nxts2 where npos in exec (ci,'cj) from label; 
            paths,:select sl:ls, tl: (exec ((ci,'cj)!(lb,'dk))from label)npos, plen:count each 1_/:-2_/:{[parent;x](2#x),parent[x]}[parent]\'[ls,'npos] from found;
        ];
        queue:select ls,pos:npos from nxts2;
    ];

    start:(`AA;1;0);
    visited:();
    queue:([]lb:enlist start 0;k:start 1;d:0;plen:0);
    while[count queue;
        minl:exec min plen from queue;
        toExpand:select from queue where plen=minl;
        if[0<count found:select from toExpand where lb=`ZZ,d=0;
            :minl;
        ];
        visited,:exec (lb,'k,'d) from toExpand;
        nxts:update npos:{[paths;x](exec (tl,'x[2],/:plen) from paths where sl~\:2#x),
            $[(x[2]>0) and (x[1]=1) and x[0]<>`ZZ;enlist(x[0];-1;x[2]-1;1);()],$[x[1]=-1;enlist(x[0];1;x[2]+1;1);()]
            }[paths]each(lb,'k,'d) from toExpand;
        nxts2:ungroup nxts;
        nxts2:update plen+last each npos, -1_/:npos from nxts2;
        nxts2:select from nxts2 where not npos in visited;
        queue:(delete from queue where plen=minl),select lb:npos[;0], k:npos[;1],d:npos[;2],plen from nxts2;
        queue:0!select first plen by lb,k,d from `plen xasc queue;
    ];
    "not found"};

.d20.test:{
    x:"                                           S         C   O       U         Z     R                                             \n                                           M         O   U       K         W     O                                             \n  #########################################.#########.###.#######.#########.#####.###########################################  \n  #.#...#...#.....#.#.#...#.............#...#.#.....#...#...#...........#.#.....#...........#...#.#...#.#.#...#.#...#...#.#.#  \n  #.###.###.#.#.###.#.###.###########.#.###.#.#.#######.###.###########.#.###.#.#######.#.#.###.#.#.###.#.#.###.#.###.###.#.#  \n  #...#.#.#...#.#.........#...#.#...#.#.......#.....#...#.#.#.#...#.........#.#.#.......#.#.......................#.........#  \n  #.###.#.#####.###.#####.###.#.###.#####.#####.#####.###.#.#.###.#.#.#.#.#####.#.###.#####.#.###.#.###.#.#####.#####.#####.#  \n  #.#.#.#...............#.......................#.#...#.#.....#...#.#.#.#.#.....#...#.#...#.#...#.#...#.#.#.#.#.#.......#.#.#  \n  #.#.#.#########.#.#.#####.#.#.#######.#######.#.#.#.#.###.#####.#.#.#####.###.#.#####.#####.#.###.#.###.#.#.###.#####.#.###  \n  #.#.#.#.....#.#.#.#.#.#...#.#.#...#.....#...#...#.#.....#.....#...#...#.#.#.#.#.........#.#.#.#...#.#...#...#.#.#...#.#.#.#  \n  #.#.#.###.###.#######.#####.###.###.#######.#.#######.###.#.###.#.#.###.###.#.###.#.#.###.#.###.#.#.#####.###.#.#.#####.#.#  \n  #.#...#.......#.........#.....#.#.#...#.#.#.....#.......#.#...#.#.#...#.........#.#.#...#.#...#.#.#.#...#...........#...#.#  \n  #.#.#####.#############.#######.#.#####.#.###.###.###########.#.#.#.#.#.#####.###.###.###.#####.#.#####.#.###.###.###.###.#  \n  #.......#...#.......#...........................#.....#.#...#.#.#.#.#.#...#.....#.#...........#.#...#.#...#.#.#.#...#.....#  \n  #####.###.###.###.###.###.#.###.#####.#.#.###########.#.#.#.#.#.#######.#.#####.#.###.#####.#########.###.#.###.#####.#####  \n  #...#.....#.#.#.#.#.#.#.#.#.#.#.#.#...#.#.#...#.........#.#.#.#.#.#.....#.#.#.#.#.#.......#.................#.#.#.....#...#  \n  ###.###.###.###.#.#.###.#####.###.#####.###.#.###.#########.#.#.#.#####.###.#.#######.#######.###.#.###.#.#.#.#.###.#####.#  \n  #.#.#.......#.................#.#.#.........#.#.......#.......#...#...............#...#.....#.#.#.#.#.#.#.#...#.....#.#...#  \n  #.#.###.###.#.#.#.###.###.###.#.#.#####.#.###.###.#.#########.#.#######.#.#.#.#######.#.#######.###.#.#######.#####.#.###.#  \n  #...#.#.#.#.#.#.#...#...#.#.#...........#.#.#.#...#.#.....#.#.#...#.#...#.#.#.#.#.................#.......#.#...#.....#...#  \n  ###.#.#.#.#.#####.#.#######.#.#.###.###.###.#.###.#####.#.###.#.###.#####.###.#.#####.###.#######.#.#######.#########.#.###  \n  #.........#...#.#.#.#.......#.#.#...#...#.#.....#.....#.#...#.#.#.....#...#.#.#.....#...#.......#.#...#.......#.#...#.....#  \n  ###.###.#######.#####.#####.#.#.#######.#.###.#.#.###.#.#.###.#.###.###.###.###.###.#.#.###############.###.###.###.###.###  \n  #.#...#.#.......#.#.#.#...#...#...#.....#.#...#.#.#.#.#.#.#...#.......#.......#.#.#...#...#.#.........#.#.#.....#.........#  \n  #.#####.#####.###.#.#####.#.###########.#.###.###.#.#.#.#.#.#.#.#####.#######.###.#.#####.#.###.#######.#.#############.###  \n  #.#.#.#...#.....#...#.......#...#.#.#.#.#.......#...#.#.#...#.#.#.......#.......#.#...#.#.#.#.#.#...#.....#.#.#.#.#.#.....#  \n  #.#.#.#.###.#.###.###.###.#.###.#.#.#.###.#.#####.#.###.#.#.###.###.#########.###.#.###.###.#.#.###.#.#####.#.#.#.#.###.###  \n  #...#...#.#.#.#.....#.#.#.#.#.......#.....#...#...#...#.#.#.#...#.......#.#.#.#...#.............#.#.#...#...#.#...........#  \n  #.#.#.###.###.###.#####.#########.###.###.###.#.#.#####.#############.###.#.#.#.#.#.###.#.#.#.###.#.#.#####.#.#.#.#.#.###.#  \n  #.#...#...#.#...#...#...#...#.#.....#.#.#.#...#.#.#.#.#.......#.#.#...#.......#.#...#.#.#.#.#.#.#.............#.#.#.#.#...#  \n  #####.#.###.#.###.#####.###.#.#.#.###.#.###.###.###.#.#####.###.#.#.#####.#.###.#####.#.###.###.#.###########.#########.###  \n  #.#.#.#.#.#.....#.#.#.#.#...#...#.#.#.#.......#.......#.#...#.....#...#...#...#.....#.#.#.#.#.#.....#...#.#.#.#.#.#.#.#...#  \n  #.#.#.#.#.#.#####.#.#.#.###.#####.#.#####.#.#.#####.###.###.###.#.#.#######.###.###.#.###.#.#.###.###.###.#.###.#.#.#.#.#.#  \n  #.......#.#.....#...........#.#.#.......#.#.#.#.....#...#.#...#.#...#.....#...#...#.......#...#.................#.......#.#  \n  #######.#.#.#####.###.#####.#.#.###.###.#####.#.#####.###.###.#.#########.#.#########.###.#######.###.#.#.###############.#  \n  #.....#.....#...#.#.#...#.....#...#.#.........#.....#.........#.........#.......#.......#.#.#.......#.#.#...#...#...#.....#  \n  ###.###.#######.###.#######.#.#.#######.#########.#######.###########.###.#########.#######.#####.###########.#.#.###.#.#.#  \n  #.#.#.......#.#.#.#.#...#.#.#.#...#    H         G       U           O   O         Z    #.#...#.......#.....#.#...#.#.#.#.#  \n  #.#.###.#.###.#.#.#.###.#.###.###.#    Y         M       K           H   U         W    #.###.#.#########.#######.#.#####.#  \n  #.......#.#.#.....#.#.......#.....#                                                     #.#.......#.#.#.#.#...............#  \n  #.#.#######.#####.#.#####.#.#.###.#                                                     #.###.#.###.#.#.#.#####.#########.#  \nTV..#...#...#.#.#.......#...#.....#..AO                                                   #.#...#...#.#.#...#.#.#...#.....#.#  \n  ###.#####.#.#.#####.#####.#######.#                                                     #.###.#.###.#.#.###.#.###.#######.#  \nZZ....#.#...#.#...#.......#.....#...#                                                     #...#.#.........#.#.#.#.#...#.....#  \n  #.###.#.#.#.#.###.###.#.###.#####.#                                                     #.#.#.#####.###.#.#.#.###.#.#.#####  \n  #.......#...........#.#.......#.#.#                                                   FA..#.....#.#.#.#...........#.#......DI\n  #####################.#.#.#.###.###                                                     ###.#####.###.#############.#######  \n  #.....#.............#.#.#.#.#.#...#                                                     #...#...............#...#.#.#......AO\n  #.#.###.#.###.#.###.#####.###.#.#.#                                                     #####.#####.#.###.#.#.#.#.###.#####  \nLU..#.....#.#...#.#.#...#.#.#.....#.#                                                   DR......#.....#.#.#.#...#.#...#.....#  \n  #####.#.#####.###.#.###.#####.###.#                                                     #.#.#####.#####.#######.#.#.#####.#  \n  #.#.#.#.....#.#...........#.....#..ZA                                                   #.#.#.......#...........#.#.......#  \n  #.#.###.#####.#####.#####.#.#.#.#.#                                                     #######.#######.#.#.#####.###.#.#.#  \n  #...#.#...#.#.....#.#.......#.#.#.#                                                     #.#...#...#.....#.#.......#...#.#.#  \n  ###.#.#####.#.#####.#########.#####                                                     #.#.#####.#########.###.#.#####.###  \n  #.........#...#...#.#.....#.#.....#                                                   CQ..#.....#.#...#.......#.#.#.#...#..FA\n  #.#######.#####.#######.#.#.#######                                                     #.###.#####.#############.#.#####.#  \nWA......#...#.#.#...#.#...#...#.....#                                                     #.#...#...#.....#.#.#.#.#...#...#.#  \n  #.#####.###.#.###.#.###.###.#.###.#                                                     #.###.#.#####.#.#.#.#.#.#####.###.#  \n  #.#.#...#.#.#.#.#.....#...#.#...#..OB                                                   #.#.......#...#.#.#.......#.#.#.#.#  \n  ###.###.#.#.#.#.#.###.###.#.###.###                                                     #.#.#.#########.#.#.#####.#.#.#.#.#  \n  #.....#.............#.....#.......#                                                     #...#.................#...........#  \n  #.#########.#.#.###.#.###.#.#.#.###                                                     #########################.#####.#.#  \n  #.....#.....#.#.#.#.#.#...#.#.#.#.#                                                     #...#...................#.#...#.#.#  \n  #.###.#####.#.#.#.#############.#.#                                                     #.#.#.#######.#.#####.#####.#.###.#  \n  #...#...#...#.#.#.....#.#.#...#.#.#                                                     #.#.#.#.....#.#...#...#.....#...#.#  \n  ###.#.###############.#.#.#.#.###.#                                                     #.#.#.###.###.#####.###.#####.#####  \nZA..#.#...#.#.#...#...#.......#...#.#                                                   KN..#.#.......#.#.......#...#.#.#....FB\n  #.#.#.###.#.#.#.###.#.###.###.###.#                                                     #.#.#####.#.#########.###.#.#.#.#.#  \n  #...#.........#.......#.#.#........SM                                                   #.#.......#.#.....#.......#.#...#.#  \n  ###########.###.#######.###########                                                     #############.#.###########.#######  \n  #.#.....#...#...#...#...#.......#.#                                                     #.........#.#.#.#.#................KL\n  #.#.#.###########.#.#.#.#.#.#####.#                                                     #.#####.#.#.###.#.###.#.#####.###.#  \nWW....#.............#...#...#........LU                                                 WW..#...#.#.............#.....#.#...#  \n  ###########.#.#####.###.#.#########                                                     #.###.###.###.###.#.#.#.#.###.###.#  \nCQ..........#.#.....#...#.#...#.....#                                                   BX..#.......#...#.#.#.#.#.#.#.#.#.#..OH\n  #.#######.###################.#.###                                                     #####.#.#####.#.###########.###.###  \n  #.#.......#.....#.#...#.......#.#..PE                                                 KL....#.#.#.#.....#...#...#...#.....#  \n  ###.#.#.###.#####.#.#####.#####.#.#                                                     #.#######.#########.#.###.#####.#.#  \n  #.#.#.#.#.....#.......#...#.....#.#                                                     #.#...#...#...#.#.#.....#.#.#...#..GM\n  #.###.###.#.#####.#.#####.#.#.###.#                                                     #.#.#.###.###.#.#.###.###.#.###.#.#  \n  #.#.......#.......#.......#.#.....#                                                     #...#...........................#.#  \n  #.#########.#.###.###.###.#####.###                                                     #####.#####.#.#####################  \n  #.........#.#.#...#.#.#.#.#...#...#                                                   RO....#.#.#...#.#...#.#.#.#.........#  \n  ###.#.#####.#####.#.#.#.#####.#####                                                     ###.###.#######.#.#.#.#.#.#.###.###  \nWJ..#.#.#...#.#...#.#...#.....#.#.#..WA                                                   #.........#.....#.......#.#.#.....#  \n  #.#.#.###.###.#.#######.#.#.#.#.#.#                                                     #.#######.#.###.###.###.#.#####.#.#  \n  #.#.#...#.....#.#...#.#.#.#.....#.#                                                     #.#.......#.#.....#.#.#...#.....#.#  \n  #.#.###.#.#.#.#####.#.###.#.#.#.#.#                                                     #.#####.###.#########.#.#.#######.#  \n  #...#.....#.#.............#.#.#...#                                                     #.#.#.............#.....#.#...#....OB\n  #.#.###.#.#.#####.###.#.#########.#    D       C       U           W       F   T        ###.#.###.###.#########.#.###.###.#  \n  #.#.#...#.#...#.....#.#...#.#.....#    I       O       I           J       B   V        #.#.....#...#.#...#.#...#.#.......#  \n  #.#####.###.#.###.###.#####.#.#.#######.#######.#######.###########.#######.###.#########.#.#######.#.#.#.#.###.###.###.#.#  \n  #.....#.#.#.#...#.#.#...#.....#.#.......#...#.#.#...........#.#.#...#.........#.#.......#.#.#...#...#...#.#.....#.#.#.#.#.#  \n  #.#####.#.#.#####.#.###########.#####.#.#.#.#.#.#####.###.###.#.#.#.###.#######.#.#.#.###.###.#######.#.###.#.#.#.#.#.###.#  \n  #.#.#.#.#.#.#.....#.#.#...#.#.#.#.#...#...#.#.......#.#...#.#.....#.#.....#...#...#.#.#.........#.#...#.#...#.#.#.....#...#  \n  ###.#.#.#.#######.#.#.#.###.#.#.#.#.#.###.#####.###.###.###.###.#####.###.###.#.###.#.#.#####.###.###.#.#.#.#.###########.#  \n  #.............#...........#.....#...#.#...#.#.#...#...#.....#...#.#...#.....#...#.#.#...#...#.......#.#.#.#.#...#.........#  \n  #####.#.#.#.#####.###############.#.#####.#.#.#.###.#######.###.#.#####.#######.#.#######.###.###.###.#.#####.###.###.###.#  \n  #.....#.#.#...#.............#...#.#...#...#.......#.#.....#.#.#...#.#.....#.#.#.#...#.......#.#.#.#...#.#.#.#...#...#.#...#  \n  ###.#.###.#####.#####.#########.#.#######.#.###.#.#####.#.#.#.###.#.###.#.#.#.#.#.###.###.#.###.###.#.###.#.#####.#.###.#.#  \n  #...#.#.......#...#.....#.......#.#.#.#...#.#...#.#.....#...#.........#.#.#...#.........#.#...#.#...#.....#...#...#.#...#.#  \n  ###.#####.#####.#######.#.###.#.###.#.#.###.#.#.#######.#######.#####.###.#.#.#.#####.#######.#.###.###.###.#######.#####.#  \n  #.#.#.#.....#...#.......#.#...#.#.#.......#.#.#...#.#...#.#.#.#.#.....#.#...#.#.....#.#.#.........#.#.#.........#...#...#.#  \n  #.###.###.###.#.#####.#.#####.###.#####.#######.###.###.#.#.#.#.###.#.#.#####.###.#####.###.#.###.###.#.#.###.#####.#.###.#  \n  #...........#.#.#.....#.#.................#.#.#.....#.......#.#.#.#.#.#.......#.......#...#.#...#.#.#...#.#.....#.......#.#  \n  #######.###.###.#.###.#####.#####.#.###.#.#.#.###.#####.###.#.#.#.#.#.###.#####.#####.#.###.###.###.#.#.#.###.###.#.#####.#  \n  #...#.....#...#.#.#...#.....#.#...#.#...#.#.......#.......#.#.....#.#.#.....#.......#...#.#.#.....#...#.#...#.#...#.....#.#  \n  ###.###.#####.#####.#.#####.#.#########.###.###.#.#####.#.###########.#.#.#####.#####.#.#.#.###########.#####.#.#.#.#.#.#.#  \n  #.#.........#...#...#.....#.#...#.#.#.#...#...#.#...#.#.#.......#.....#.#...#.......#.#.#.#.#.....#.#.....#...#.#.#.#.#.#.#  \n  #.#####.#####.#######.#########.#.#.#.#.#######.###.#.#.#############.#.#.#####.###.###.#.###.#####.###.###.###.#.#.#.#.###  \n  #...........#.....#.#.#.................#.#.......#...#...#...#.....#.#.#.#.#.#.#.#.#.#.#...#...#.#.......#...#.#.#.#.#...#  \n  #.###.#.###########.#.###.#.###.#.#.#.#.#.#.#####.#####.#.###.###.#.#.#.###.#.#.#.###.###.#####.#.###.#.#.#######.#.#######  \n  #...#.#.......#.#...#.#.#.#.#.#.#.#.#.#.#.....#.....#...#...#.#.#.#.#.#.....#.......#.....#...#.#...#.#.#.....#...#.#.....#  \n  #.#.###.#######.###.###.###.#.#########.###########.###.#####.#.#.#.#.#.#######.###.#.#####.###.#.#.#####.###.###.#####.###  \n  #.#.#.#.......#.#.......#...#.......#.....#.#.#...#.#.#.#.....#...#...#.....#.#...#.#.............#.#.#.#.#.#...#.......#.#  \n  #.###.#.###.###.#.###.#.#########.#.###.###.#.#.#.#.#.#.#.###.#.#######.#####.#.#####.#####.###.#####.#.###.#.#####.#.#.#.#  \n  #...#.....#.#.#.#.#...#...#.#.#...#...#.#.......#.#.#.....#...#.....#.#...#...#.#.#...#...#.#.#.......#.#...#...#...#.#...#  \n  #####.###.###.#.#######.#.#.#.#####.###.#######.#.#.#######.#####.#.#.#.#####.#.###.#.#.#####.#.#######.###.#######.###.###  \n  #.#...#...#...........#.#...#...........#...#...#.....#.....#.#.#.#...#.....#...#...#...#.#.#.#.#.............#...#.#.#...#  \n  #.#.#######.#.#.###.#.###.#.###########.#.#####.###########.#.#.#####.#.###.###.#.###.#.#.#.#.#####.###.#.#.###.###.#.#.###  \n  #.......#...#.#.#.#.#.....#.#...#.........#.....#.......#...#.....#...#.#.#.#...#...#.#.#.#.#.......#.#.#.#.......#.#.....#  \n  #.#######.#.#.#.#.###.#####.#.###########.#####.#.###.#####.#.#######.#.#.#####.###.#.###.#.#.###.###.#.#.###.#.#####.###.#  \n  #...#.....#.#.#.#.....#...........#.#.#...#...#...#.#.....#.....#.....#...#.#.......#...........#.....#.#.#.#.#...#.....#.#  \n  #.###.#.#########.#.#.#.#.#.###.#.#.#.#.###.#.#####.#.###.###.#####.#####.#.#.#.#.###.###.###.###.#########.#.###.#.#.#.#.#  \n  #...#.#...#.......#.#.#.#.#.#...#...........#.#.........#.#.....#.....#.....#.#.#.#...#.....#...#...........#...#.#.#.#.#.#  \n  #############################################.#.#####.#######.###.#####.###########.#######################################  \n                                               B A     H       P   K     D           U                                         \n                                               X A     Y       E   N     R           I                                         ";
    show d20p1 x;
    show d20p2 x;
    };

/
OVERVIEW:

PART 1:
Finding the portal locations is a bit complicated. Using some vector shifting, we
overlay 3 layers of the input to find where there is a combination of two letters
and a dot in a particular direction, and use this to construct the portal list.
The path search is a vector BFS that I have already used on previous puzzles, except
now we have to account for portal warps being a valid move in addition to the normal
up/left/down/right transitions.

PART 2:
The depth is now part of the state in addition to the two coordinates. However the
same BFS would be too slow, so we use the same trick as day 18: using simultaneous
BFS's to calculate paths between portals and only store their endpoints and lenghts.
There is only one BFS this time as there is no dependency to keep track of.
Once we have the simplified graph we use Dijkstra's algorithm to find the path from
(AA;0) to (ZZ;0). Once again portal moves need to be incorporated into the allowed
moves in addition to the edges of the simplified graph. Moves to negative levels
and moves through ZZ need to be specifically excluded (ZZ is only checked as the
exit condition). (Not excluding AA is not a problem because there will be no edge
coming out of the non-existing exit. But in an early version of my code, the lack
of an explicit check for ZZ caused the code to cheat by moving through ZZ on depth
1 since that caused it to arrive at (ZZ;0), which triggered the exit condition while
it was invalid due to being at the non-existing end of the ZZ portal.)
